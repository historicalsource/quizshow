	.TITLE QUIZ SHOW 17-FEB-76 TAH
	.ASECT
	.=20000
TYMA=17753
TYMB=17754
BTYC=17755
DATA=0
INDEX=1
CRLF=36
HI=0
LO=2
MATCH=0
START=0
R0=0
R1=1
R2=2
R3=3
EQU=0
GRTR=1
LESS = 2
UNC=3
ZERO = 0
POS = 1
NEG = 2
DISP = 17000
DP = DISP
P1A = 17760
HOLD1 = 17743
BONNUM = 14210
SCOR1 = DISP+45
SCOR2 = DISP+67
ANSW = DISP+740
TIME = DISP+741
MIN =  DISP+774
MOUT = DISP+776
DPTR = DISP+772
PGAM = DISP+744
TMP1 = DISP+745
AUDIO = 14002
ANLMP1 = 14004
ANLMP2 = 14010
STLMPS = 14020
CATSEL = 14040
TPMTR  = 14040
VIDIS = 14100
HEDPOS = 14201
START1 = 14201
COIN1 = 14201
ANSWER = 14202
GAMLEN = 14204
TWONE = 14204
TIMCNT = 14204
LAMPT = 14204
FIFSIX = 14204
SYNC = 14400
EOT = 340
BONUS = DISP+747
DONE = DISP+750
SYNC8 = DISP+746
P1STAT = DISP+751
P2STAT = DISP+752

MAINLP:	EORZ R0			;RESET ALL FUNCTIONS
	LPSL
	STRA R0,CATSEL		;TURN OFF CAT SELECT & TAPE POWER
	LODI R0,60
	LODI R1,6
	STRA R1,X,P1A-1
	BDRR R1,S,-3
MAINLQ:	LODI R2,1
	LODI R1,0
	STRA R1,SYNC8		;RESET SYNC8
	STRA R1,CATSEL
	STRA R1,STLMPS
	STRA R1,BONUS
	STRA R1,DONE
	LODI R0,40
	STRA R1,X,DISP
	STRA R1,X,DISP+340
	BIRR R1,S,-6
	STRA R1,VIDIS		;ENABLE VIDEO
	LODA R1,P1A		;TEST P1 SCORE
	ANDI R1,17
	COMI R1,3
	BCTR LESS,S,+4
	LODI R1,3
	ANDI R1,3
	RRL R1
	ADDI R1,50
	BSTA UNC,MESOUT		;P1 RATING OUT
	PPSL 20
	LODZ R3
	CPSL 20
	COMI R0,2
	BCTR LESS,S,+25
	LODA R1,P1A+3
	ANDI R1,17
	COMI R1,3
	BCTR LESS,S,+4
	LODI R1,3
	ANDI R1,3
	RRL R1
	ADDI R1,60
	BSTA UNC,MESOUT		;P2 RATING OUT
	PPSL 20
	LODI R3,0
	CPSL 20
	LODI R1,46
	BSTA UNC,MESOUT		;"GAME OVER"
	BSTA UNC,PSCR		;PLAYER HEADING
	LODI R1,30		;1ST ROW OF ASTK.
	BSTA UNC,MESOUT
	LODI R1,36		;"THE KEE GAMES QUIZ SHOW"
	BSTA UNC,MESOUT
	LODI R1,32		;2ND ROW OF ASTK.
	BSTA UNC,MESOUT
	LODI R1,72		;TEST YOUR KNOWLEDGE
	BSTA UNC,MESOUT
	LODI R1,74		;TEST A FRIEND'S
	BSTA UNC,MESOUT
	LODI R1,6
	BSTA UNC,MESOUT		;25 CENTS PER GAME
	LODA R0,TWONE
	TMI  R0,20
	BCTR ZERO,CNLP1
	LODI R1,40
	BSTA UNC,MESOUT		;PLAYER

CNLP1:	LODI R1,157
	BSTA UNC,COININ
	PPSL 20
	LODZ R3
	CPSL 20
	BRNR R0,STSRCH
	BDRR R2,CNLP1+2
	BDRR R1,CNLP1+2
	LODA R0,SYNC
	STRA R0,ANLMP1		;SYNC TO ANSWER LAMPS
	EORI R0,377
	STRA R0,ANLMP2
	BCTR UNC,CNLP1
STSRCH:	LODI R1,6
B9:	LODA R0,SYNC
	TMI R0,20		;LOOK FOR HIGH
	BCFR ZERO,B9
B10:	LODA R0,SYNC
	ANDI R0,20		;LOOK FOR LOW
	BRNR R0,B10
B11:	BDRR R0,B11
	BDRR R1,B11		;18.4 MSEC WAIT
	LODA R1,SYNC
	ANDI R1,20		;R1 DOES NOT = 0 IF 60 CYCLES, R1=0 IF 50 CYCLES
	BCTR ZERO,SET
SET50:	LODA R0,FIFSIX
	BCFA NEG,CNLP1
SET:	BSTA UNC,SYN
	LODA R0,KCD
	COMA R0,DISP+250	;CHECK "K" ON SCREEN
	BCFA ZERO,MAINLQ
	LODA R0,ECD
	COMA R0,DISP+251	;CHECK "E" ON SCREEN
	BCFA ZERO,MAINLQ
	COMA R0,DISP+252	;CHECK "E" ON SCREEN
	BCFA ZERO,MAINLQ
	LODI R0,14
	STRA R0,TPMTR		;DECK ON
	BSTA UNC,COININ		;TEST COIN IN
	LODI R0,1
	STRA R0,VIDIS		;DISABLE VIDEO
	LODI R0,40		;CLEAR 3RD QUARTER OF SCREEN
	LODI R1,200
	STRA R1,X,DISP+377
	BDRR R1,S,-3
	STRA R1,ANLMP2		;CLEAR PLAYER 2'S LAMPS
	BSTA UNC,COININ		;TEST COIN IN
	LODI R0,40		;CLEAR 4TH QUARTER OF SCREEN
	LODI R1,200
	STRA R1,X,DISP+537
	BDRR R1,S,-3
	BSTA UNC,COININ		;TEST COIN IN
	LODI R0,40
	LODI R1,100
	STRA R1,X,DISP+77
	BDRR R1,S,-3
	STRA R1,VIDIS		;ENABLE VIDEO
	LODI R2,1
	LODI R1,12
	BSTA UNC,MESOUT		;"CHOOSE A CATEGORY" OUT
	LODA R0,TWONE
	TMI R0,20		;TEST CHEAP
	BCTR ZERO,CHP
	LODI R1,14
	BSTA UNC,MESOUT		;"PUSH 1 PLAYER START"
	LODI R0,1
	STRA R0,STLMPS		;START 1 LIGHT
	PPSL 20
	TMI R3,2
	BCFR ZERO,CHPNOT
	CPSL 20
	LODI R0,62		;"2" TO R0
	STRA R0,DISP+513	;"2" TO SCREEN
	LODI R0,2
	STRA R0,STLMPS		;START 2 LIGHTS
	BCTR UNC,CHPNOT
CHP:	LODI R0,3
	STRA R0,STLMPS		;START BUTTONS LIGHT
	LODI R1,16
	BSTA UNC,MESOUT		;"PUSH 1 OR 2 PLAYER START" TO SCREEN
CHPNOT:	CPSL 20
	LODI R1,76		;RIGHT ANSWER GETS Q.T. POINTS
	BSTA UNC,MESOUT
	LODI R1,100		;2 SEC. PENALTY FOR WRONG ANSWER
	BSTA UNC,MESOUT
	LODI R1,24
	BSTA UNC,MESOUT		;"CATEGORY:" TO SCREEN
STLP1:	LODA R1,HEDPOS		;HEAD POSITION TO R1
	ANDI R1,17
	BCTR ZERO,S,+10
	RRL R1
	LODI R2,1
	BSTA UNC,MESOUT		;CATEGORY NAME TO SCREEN
	LODA R0,START1
	TMI  R0,20		;TEST START 1
	BCTR ZERO,STRT1
	TMI  R0,40		;TEST START 2
	BCTR ZERO,STRT2
	LODA R0,TWONE
	TMI R0,20
	BCTR ZERO,STLP1
	PPSL 20
	LODZ R3
	CPSL 20
	COMI R0,2		;TEST COIN CAME IN
	BCFR ZERO,STLP1
	LODI R0,62
	STRA R0,DISP+513	;"2" TO SCREEN
	LODI R0,2
	STRA R0,STLMPS		;START 2 LIT
	BCTR UNC,STLP1		;WAIT FOR START PUSH
STRT1:	PPSL 20
	LODZ R3
	CPSL 20
	TMI  R0,2		;TEST FOR 2 COINS
	BCTA ZERO,STLP1
	LODI R0,40
	LODI R1,13
	STRA R1,X,DISP+24
	STRA R1,X,DISP+64
	BDRR R1,S,-6
	BCTR UNC,GAMEST		;START BUTTON PUSHED-1 PLAYER
STRT2:	PPSL 20
	LODZ R3
	CPSL 20
	TMI  R0,2		;TEST FOR 2 COINS
	BCTR ZERO,GAMEST
	LODA R0,TWONE
	TMI R0,20		;TEST CHEAP
	BCFA ZERO,STLP1
	PPSL 20
	LODI R3,2
	CPSL 20
	LODA R0,KCD
	COMA R0,MES36+7
	BCFA ZERO,MAINLP
GAMEST:	EORZ R0
	STRA R0,STLMPS		;CLEAR START LIGHTS
	BSTA UNC,SYN		;CLEAR START BUTTON MESSAGE
	LODI R0,40
	LODI R1,300
	STRA R1,X,DISP+137	;CLEAR GAME OVER & BELOW
	STRA R1,X,DISP+437
	BDRR R1,S,-6
	LODA R0,BONNUM
	ANDI R0,17
	BCTR ZERO,BONDON
	LODI R1,42
	BSTA UNC,MESOUT
	LODA R0,BONNUM
	ANDI R0,17
	IORI R0,260
	STRA R0,DISP+500
	PPSL 20
	LODZ R3
	CPSL 20
	TMI R0,2		;TEST FOR 2 PLAYER
	BCTR ZERO,BON2
	LODI R1,44
	BSTA UNC,MESOUT		;"FOR A SCORE OF 4000" TO SCREEN
	BCTR UNC,BONDON
BON2:	LODI R1,22
	BSTA UNC,MESOUT		;"FOR A SCORE OF 3000" TO SCREEN
BONDON:	PPSL 20
	LODZ R3
	CPSL 20
	LODI R1,17
	TMI  R0,2		;TEST 2-PLAYER
	BCFR ZERO,S,+5
	STRA R1,ANLMP2		;SET PLAYER 2
	STRA R1,ANLMP1		;SET PLAYER 1
	BSTA UNC,SYN		;PLAYER & SCORE OF 0 TO SCREEN
	LODI R2,60
	LODI R1,4
	LODZ R2
	STRA R1,X,DISP+44
	PPSL 20
	LODZ R3
	CPSL 20
	TMI R0,2
	BCTR ZERO,S,+10
	LODZ R2
	STRA R1,X,P1A+2
	BCTR UNC,S,+6
	LODZ R2
	STRA R1,X,DISP+66
	BDRR R1,S,-27
GAMCYC:	LODA R1,HEDPOS
	ANDI R1,17
	BCTR ZERO,S,+6
	RRL R1
	BSTA UNC,MESOUT		;OUTPUT CATEGORY NAME
	LODA R0,TIMCNT
	TMI R0,40		;TEST GAME TIME OR COUNT
	BCTR ZERO,GTIM
	LODI R1,5		;DEFAULT OF 5 QUESTIONS
	LODA R0,GAMLEN
	ANDI R0,17		;SELECT GAME LENGTH BITS
	ADDZ R1
	STRA R0,TIME		;SAVE GAME TIME
	BCTR UNC,STGAME-4
GTIM:	EORZ R0
	STRA R0,TIME+1		;SET-UP INITIAL GAME TIME
	LODA R0,GAMLEN
	ANDI R0,17
	RRL  R0
	RRL  R0
	RRL  R0
	ADDI R0,66		;DEFAULT 1 MINUTE (MINIMUM)
	STRA R0,TIME
	LODI R2,377
	BDRR R0,S,0
	BDRR R2,S,-2
	LODI R2,377
	BCTR UNC,STGAME+2
STGAME:	LODI R2,40
	LODI R0,10
	STRA R0,CATSEL		;ENABLE CATEGORY SELECT
CAT1:	LODA R1,HEDPOS
	ANDI R1,17
	BCTR ZERO,CAT1
	RRL R1
	BSTA UNC,MESOUT		;CATEGORY OUT
	BSTA UNC,TIMER
	BDRR R2,CAT1
	PPSL 20
	STRA R3,HOLD1
	CPSL 20
	SPSU
	STRA R0,PGAM
	BSTA UNC,QUEST		;GET QUESTION FROM TAPE TO SCREEN
	PPSL 20
	LODA R3,HOLD1
	LODI R1,0
	LODI R2,0
	CPSL 377		;CLEAR PSW
	LODA R0,PGAM
	LPSU
	TMI  R0,100		;TEST TCHAN
	BCTR ZERO,S,+6
	PPSU 100		;SET TCHAN
	BCTR UNC,S,+4
	CPSU 100		;CLEAR TCHAN
	LODA R0,ECD
	COMA R0,MES36+11
	BCFA ZERO,MAINLP
	LODI R1,26
	BSTA UNC,MESOUT		;"QUESTION TIMER:" TO SCREEN
	LODI R0,14
	STRA R0,CATSEL		;ENABLE CATEGORY SELECT
ANSLP1:	LODI R2,340
	LODA R0,ANSWER
	BRNA R0,ANSGIV		;TEST FOR ANSWER BUTTONS ACTIVE
ANSLP2:	CPSL 20
	BSTA UNC,TIMER
	LODA R1,HEDPOS
	ANDI R1,17
	BCTR ZERO,ANSLP2
	RRL R1
	BSTA UNC,MESOUT		;CATEGORY OUT
	BSTA UNC,BADSND
	LODI R0,100
	BDRR R0,S,0
	LODA R0,SYNC
	TMI R0,4		;TEST 4R ACTIVE
	BCFA ZERO,ANSLP1+2
	BIRR R2,ANSLP1+2
	BSTA UNC,TIC		;MAKE TIC SOUND
	BSTA UNC,TIMER
	BSTA UNC,SYN
	LODA R0,DISP+167	;DECREMENT QUESTION TIMER
	ANDI R0,7
	BCTR ZERO,DECUPR
	LODI R0,60
	STRA R0,DISP+167
	LODA R0,DISP+166
	ANDI R0,7
	BCTR R0,QTOVER
	BCTA UNC,ANSLP1
DECUPR:	LODI R0,65
	STRA R0,DISP+167
	LODA R0,DISP+166
	BDRR R0,S,+2
	STRA R0,DISP+166
	BCTA UNC,ANSLP1
QTOVER:	BSTA UNC,TIMER
	BSTA UNC,BAD		;GENERATE BAD SOUND & SHOW CORRECT ANSWER
	LODI R1,3
	BSTA UNC,CLRANS
	LODI R1,17
	STRA R1,ANLMP1		;RESET P1 LAMPS
	PPSL 20
	LODZ R3
	CPSL 20
	TMI R0,2
	BCFR ZERO,S,+12
	STRA R1,ANLMP2		;RESET P2 LAMPS
	LODI R1,25
	BSTA UNC,CLRANS
	BSTA UNC,SYN		;RE-SYNC TO VIDEO
	LODA R0,BONUS
	BCTR NEG,QCOUNT
	LODA R0,TIMCNT
	TMI R0,40
	BCFR ZERO,QCOUNT
	LODA R0,TIME
	SUBI R0,3
	BCFR NEG,S,+7
	LODI R0,200
	STRA R0,DONE
	STRA R0,TIME
	LODA R0,DONE
	BRNA R0,GAMOUT
	LODA R0,ECD
	COMA R0,MES36+10
	BCFA ZERO,MAINLP
	BSTA UNC,TIMER
	LODA R0,DONE
	BRNA R0,GAMOUT
	BCTA UNC,STGAME
QCOUNT:	BSTA UNC,SYN		;RE-SYNC TO VIDEO
	LODA R0,TIME
	SUBI R0,1
	STRA R0,TIME
	BRNA R0,STGAME
	BCTA UNC,GAMOUT
ANS2:	TPSU 100
	BCFA ZERO,ANSP2
	BCTA UNC,ANSLP2
ANSGIV:	COMI R0,377		;TEST FOR ALL ANSWER BUTTONS
	BCFR EQU,S,+7
	LODI R1,110		;KEE GAMES TO SCREEN BETWEEN SCORES
	BSTA UNC,MESOUT
	PPSL 20			;SELECT BANK 1
	TPSU 100
	BCTA ZERO,ANSP2		;ALTERNATE CHECKING P1 OR P2
ANSP1:	BRNR R1,ANS2
	LODA R0,ANSWER
	ANDI R0,17
	BCTR ZERO,ANS2		;NOT PLAYER 1, TEST PLAYER 2
	CPSL 20
	BSTA UNC,ANSDEV		;GET ANSWER CODE  TO R1
	COMA R1,ANSW
	BCFR ZERO,P1ERR
	STRA R0,ANLMP1
	LODI R1,102
	BSTA UNC,MESOUT		;RIGHT TO P1
	LODI R3,47
	BSTA UNC,SCORE		;INCREMENT PLAYER 1 SCORE
	BSTA UNC,GOOD		;INVERT CORRECT ANSWER & MAKE GOOD SOUND
	LODI R1,3
	BSTA UNC,CLRANS
	EORZ R0
	PPSL 20
	STRZ R2
ANSEND:	CPSL 20
	BSTA UNC,TIMER
	BSTA UNC,BONTST		;TEST BONUS
	LODA R1,DONE		;TEST GAME TIME OVER
	BCTA NEG,GMOUT
	BCTA UNC,QTOVER+6	;RECYCLE-PLAYER GOT ANSWER
ANS1:	TPSU 100
	BCTA ZERO,ANSP1
	BCTA UNC,ANSLP2
P1ERR:	EORI R0,377
	STRA R0,ANLMP1
	LODI R1,0
	BSTA UNC,MESOUT		;WRONG TO P1
	PPSL 20
	LODI R1,77
	BCTA UNC,ANS2
ANSP2:	BRNR R2,ANS1
	TMI R3,2
	BCFR ZERO,ANS1
	LODA R0,ANSWER
	ANDI R0,360		;SELECT P2 ANSWER
	BCTA ZERO,ANS1
	RRR  R0
	RRR  R0
	RRR  R0
	RRR  R0
	CPSL 20
	BSTA UNC,ANSDEV		;GET ANSWER CODE TO R1
	COMA R1,ANSW		;CHECK FOR CORRECT ANSWER
	BCFR ZERO,P2ERR
	STRA R0,ANLMP2
	LODI R1,34
	BSTA UNC,MESOUT		;RIGHT TO P2
	LODI R3,71
	BSTA UNC,SCORE		;INCREMENT PLAYER 2 SCORE
	BSTA UNC, GOOD		;INVERT CORRECT ANSWER & MAKE GOOD SOUND
	LODI R1,25
	BSTA UNC,CLRANS
	EORZ R0
	PPSL 20
	STRZ R1
	BCTA UNC,ANSEND
P2ERR:	EORI R0,377
	STRA R0,ANLMP2
	LODI R1,104
	BSTA UNC,MESOUT		;WRONG TO P2
	PPSL 20
	LODI R2,77
	BCTA UNC,ANS1		;CONTINUE COUNT DOWN
GAMOUT:	BSTA UNC,SYN		;RE-SYNC TO VIDEO
	LODI R0,200
	STRA R0,DONE		;SET DONE FLAG
	LODA R0,BONUS
	BCTR NEG,GMOVR
	BSTA UNC,BONTST		;SET-UP BONUS-IF THERE
GMOUT:	BSTA UNC,SYN		;RE-SYNC TO VIDEO
	LODA R1,BONUS
	BCTR ZERO,GMOVR
	LODI R0,200
	STRA R0,BONUS
	EORZ R0			;SET-UP FOR BONUS TIME
	STRA R0,DONE
	LODI R1,17
	PPSL 20
	LODZ R3
	CPSL 20
	TMI R0,2		;TEST 2-PLAY
	BCFR ZERO,S,+5
	STRA R1,ANLMP2
	STRA R1,ANLMP1
	BCTA UNC,STGAME		;RESTART BONUS GAME
GMOVR:	LODI R0,1
	STRA R0,VIDIS		;DIS VID
CLRSCN:	LODI R1,3
	LODA R1,X,DISP+44
	STRA R1,X,P1A-1
	LODA R1,X,DISP+66
	COMI R0,40		; TEST FOR P1 ONLY
	BCTR EQU,S,+5
	STRA R1,X,P1A+2
	BDRR R1,S,-20
	BCTA UNC,MAINLQ

				;SUBROUTINES START HERE

PSCR:	LODI R0,60
	STRA R0,DISP+50
	STRA R0,DISP+72
	LODI R1,3
	LODA R1,X,P1A-1
	STRA R1,X,DISP+44
	LODA R1,X,P1A+2
	STRA R1,X,DISP+66
	BDRR R1,S,-14
	RETC UNC


				;R1=OFFSET FOR PLAYER - 1
CLRANS:	BSTA UNC,SYN
	LODI R0,40
	LODI R3,7
	STRA R1,P,DISP
	BDRR R3,S,-3
	RETC UNC
COININ:	LODA R0,COIN1
	ANDI R0,300
	RETC ZERO
	PPSL 20
	LODI R1,20
	LODI R2,20
COINLP:	CPSL 20
	LODA R0,COIN1		;CHECK FOR COIN
	ANDI R0,300
	RETC ZERO
	LODI R3,120		; APPR. 1 MS WAIT
	BDRR R3,S,0
	PPSL 20
C1IN:	TMI R0,100		;CHECK COIN 1
	BCFR EQU,C2IN
	BDRR R1,C2IN		; DEC. TIMER
	BIRR R3,C2IN		; INC. COIN COUNT
C2IN:	TMI R0,200		; CHECK COIN 2
	BCFR EQU,COINLP
	BDRR R2,COINLP		;DEC. TIMER
	BIRR R3,COINLP

TIC:	LODI R0,6
	LODI R1,4
	STRA R0,AUDIO
	STRA R0,AUDIO
	STRA R0,AUDIO
	BDRR R1,S,-11
	BDRR R0,S,-15
	RETC UNC

SYN:	LODA R0,SYNC
	BCTR NEG,S,-3
	LODA R0,SYNC
	BCFR NEG,S,-3
	RETC UNC

MESOUT:	BSTR UNC,SYN
	LODA R1,X,MINTBL	;GET START ADDR. OF MESSAGE IN MEMORY
	STRA R0,MIN
	LODA R1,X,MINTBL+1
	STRA R0,MIN+1
	LODA R1,X,MOUTBL	;GET START ADDR. OF SCREEN LOCATION
	STRA R0,MOUT
	LODA R1,X,MOUTBL+1
	STRA R0,MOUT+1
	LODI R1,377
MST1:	BSTA UNC,COININ		;TEST FOR COIN
	BSTA UNC,SYN
MST2:	LODA R1,NP,MIN		;CHAR TO R0
	COMI R0,EOT		;EOT?
	RETC ZERO
MSOUT:	STRA R1,NX,MOUT		;CODE TO SCREEN
	BRNR R2,MST1
	BCTR UNC,MST2

ANSDEV:	LODI R1,0
	TMI R0,1		;TEST ANSWER A
	BCFR ZERO,TSTB
	LODI R1,1
TSTB:	TMI R0,2		;TEST ANSWER B
	BCFR ZERO,TSTC
	COMI R1,0		;TEST MULTIPLE ANSWERS
	BCFR ZERO,ERRANS
	LODI R1,2
TSTC:	TMI R0,4		;TEST ANSWER C
	BCFR ZERO,TSTD
	COMI R1,0		;TEST MULTIPLE ANSWERS
	BCFR ZERO,ERRANS
	LODI R1,3
TSTD:	TMI  R0,10		;TEST ANSWER D
	BCFR ZERO,TSTOUT
	COMI R1,0		;TEST MULIPLE ANSWERS
	BCFR ZERO,ERRANS
	LODI R1,4
TSTOUT:	RETC UNC		;RETURN ANSWER CODE IN R1
ERRANS:	LODI R1,7		;ERROR CODE IN R1
	RETC UNC		;RETURN ERROR CODE IN R1

SCORE:	BSTA UNC,SYN		;RE-SYNC
	LODA R1,DISP+167
	ANDI R1,17
	LODA R3,X,DISP
	ADDZ R1
	STRA R3,X,DISP
	COMI R0,72
	BCTR LESS,S,+16
	SUBI R0,12
	STRA R3,X,DISP
	BSTA UNC,SYN
	LODI R0,1
	BCTR UNC,S,+6
	BSTA UNC,SYN
	EORZ R0
	LODA R1,DISP+166
	ANDI R1,17
	ADDA R3,X,DISP-1
	ADDZ R1
	STRA R3,X,DISP-1
	COMI R0,72
	RETC LESS
	SUBI R0,12
	STRA R3,X,DISP-1
	LODI R0,1
	ADDA R3,X,DISP-2
	STRA R3,X,DISP-2
	COMI R0,72
	RETC LESS
	LODI R0,60
	STRA R3,X,DISP-2
	RETC UNC

INVERT:	BSTA UNC,SYN		;RE-SYNC
	LODA R0,ANSW
	LODI R3,40
	BDRR R0,S,+4
	BCTR UNC,S,+6
	ADDI R3,100
	BCTR UNC,S,-6
	LODI R2,40
	BSTA UNC,SYN		;RE-SYNC
	LODA R3,X,DISP+377
	IORI R0,200		;INVERT CODE
	STRA R3,X,DISP+377
	BDRR R3,S,+2
	BDRR R2,S,-12
	RETC UNC

BAD:	BSTR UNC,INVERT		;INVERT CORRECT ANSWER
	LODI R2,0		;GENERATE BAD SOUND
	LODI R3,0
BAD1:	BDRR R3,S,0
	LODZ R2
	RRR R0
	BCFR NEG,BAD2
	LODI R1,2
	STRA R1,AUDIO
	BCTR UNC,BAD3
BAD2:	LODI R1,0
	STRA R1,AUDIO
BAD3:	BDRR R2,BAD1
	LODI R1,0
	STRA R1,AUDIO
	RETC UNC

GOOD:	BSTA UNC,INVERT		;INVERT ANSWER CODE
	LODI R1,7
GOOD1:	LODI R2,0
GOOD2:	LODI R3,100
	BDRR R3,S,0
	LODZ R1
	ANDZ R2
	RRR  R0
	BCFR NEG,GOOD3
	LODI R0,2
	BCTR UNC,S,+3
GOOD3:	EORZ R0
	STRA R0,AUDIO
GOOD4:	BDRR R2,GOOD2
	BDRR R1,GOOD1
	EORZ R0
	STRA R0,AUDIO
	RETC UNC

KCD:	.BYTE 213
BADSND:	PPSL 20
	LODZ R1
	BCTR ZERO,P2BAD
	BDRR R1,S,+2
	BRNR R1,S,+20
	CPSL 20
	LODI R1,3
	BSTA UNC,CLRANS
	LODI R0,17
	STRA R0,ANLMP1		;RESET PLAYER 1 LAMPS
	PPSL 20
	LODZ R2
	BRNR R0,P2BADA
SOUND:	CPSL 20			;SELECT BANK 1
	LODI R1,40
SOUNDA:	TMI R1,1		;FREQ COUNT IN R2
	BCTR ZERO,SOUNDB
	LODI R0,2
SOUNDC:	STRA R0,AUDIO
	LODI R3,100
	BDRR R3,S,0		;DURATION COUNT IN R3
	BDRR R1,SOUNDA
	CPSL 20			;SELECT BANK 0
	BIRR R2,S,+4
	LODI R2,377
	RETC UNC
SOUNDB:	EORZ R0
	BCTR UNC,SOUNDC
P2BAD:	LODZ R2
	CPSL 20
	RETC ZERO		;TEST P2 BAD SOUND
P2BADA:	PPSL 20
	BDRR R2,S,+2
	BRNR R2,SOUND
	CPSL 20
	LODI R1,25
	BSTA UNC,CLRANS
	LODI R0,17
	STRA R0,ANLMP2		;RESET PLAYER 2 LAMPS
	BRNR R2,SOUND
	BCTR UNC,SOUNDB

BONTST:	BSTA UNC,SYN		;RE-SYNC
	LODA R0,BONNUM		;CHECK BONUS SETTINGS
	ANDI R0,17
	BRNR R0,S,+10
NOBON:	LODI R0,0
	STRA R0,BONUS
	RETC UNC
	LODA R0,BONUS
	RETC NEG
	BCTR ZERO,S,+4
	BCTR UNC,OKBON
	LODA R2,DISP+45		;P1 SCORE TO R2
	LODA R3,DISP+67		;P2 SCORE TO R3
	ANDI R2,17
	ANDI R3,17
	PPSL 20
	LODZ R3
	CPSL 20
	TMI R0,2		;TEST FOR 2 PLAYER
	BCTA ZERO,BON2P
	COMI R2,4		;>3999 FOR SINGLE
	BCTR LESS,NOBON
OKBON:	LODA R0,BONNUM
	ANDI R0,17
	LODA R1,DONE
	BRNR R1,BONPLY
	LODA R1,BONUS
	BCTR ZERO,S,+3
	RETC UNC
	STRA R0,BONUS
	LODI R1,70
	BSTA UNC,MESOUT		;"BONUS" OUT
	RETC UNC
BONPLY:	STRA R0,TIME
	LODI R0,200
	STRA R0,BONUS
	LODI R1,5
	STRA R1,VIDIS		;DISABLE VIDEO
	LODA R1,X,DISP+14
	IORI R0,100
	STRA R1,X,DISP+14
	BDRR R1,S,-10
	LODI R0,40
	STRZ R2
	LODI R1,0
	STRA R1,X,DISP+200
	STRA R1,X,DISP+340
	BIRR R1,S,-6
	STRA R1,VIDIS		;ENABLE VIDEO
	LODI R1,106		;BONUS QUESTIONS
	BSTA UNC,MESOUT
	BSTA UNC,WHOOP
	RETC UNC
BON2P:	COMI R2,3		;>2999 SCORE FOR P1?
	BCFA LESS,OKBON
	COMI R3,3		;>2999 SCORE FOR P2?
	BCFA LESS,OKBON
	BCTA UNC,NOBON
ECD:	.BYTE 205
TIMER:	BSTA UNC,SYN
TIMERA:	LODA R0,BONUS
	RETC NEG
	LODA R0,TIMCNT
	TMI R0,40
	BCTR ZERO,S,+3
	RETC UNC
	LODA R0,SYNC
	ANDI R0,100
	BCFR ZERO,S,+6
	STRA R0,SYNC8
	RETC UNC
	EORA R0,SYNC8
	RETC ZERO
	STRA R0,SYNC8
	BSTA UNC,SYN		;RE-SYNC
	LODA R0,TIME+1
	BRNR R0,DECL
	LODA R0,TIME
	BRNR R0,DECU
	LODI R0,200
	STRA R0,DONE
	RETC UNC
DECU:	BDRR R0,S,+2
	STRA R0,TIME
	LODI R0,300
	STRA R0,TIME+1
	RETC UNC
DECL:	SUBI R0,100
	STRA R0,TIME+1
	RETC UNC

WHOOP:	LODI R1,10		;WHOOP COUNT
	LODI R2,140		;PRESET HALF CYCLE TIMER
WHB:	LODI R3,6		;PRESET CYCLE COUNTER
WHC:	LODZ R2
WHD:	BDRR R0,WHD		;TIMER LOOP
	LODZ R3
	RRL  R0
	STRA R0,AUDIO		;SOUND = LSB OF R3
	BDRR R3,WHC
	BDRR R2,WHE
WHE:	COMI R2,20
	BCFR EQU,WHB
	BDRR R1,WHOOP+2
	STRA R1,AUDIO
	RETC UNC
MINTBL:	.BYTE MES0&17400/400,MES0&377
	.BYTE MES2&17400/400,MES2&377
	.BYTE MES4&17400/400,MES4&377
	.BYTE MES6&17400/400,MES6&377
	.BYTE MES10&17400/400,MES10&377
	.BYTE MES12&17400/400,MES12&377
	.BYTE MES14&17400/400,MES14&377
	.BYTE MES16&17400/400,MES16&377
	.BYTE MES20&17400/400,MES20&377
	.BYTE MES22&17400/400,MES22&377
	.BYTE MES24&17400/400,MES24&377
	.BYTE MES26&17400/400,MES26&377
	.BYTE MES30&17400/400,MES30&377
	.BYTE MES30&17400/400,MES30&377
	.BYTE MES34&17400/400,MES34&377
	.BYTE MES36&17400/400,MES36&377
	.BYTE MES40&17400/400,MES40&377
	.BYTE MES42&17400/400,MES42&377
	.BYTE MES44&17400/400,MES44&377
	.BYTE MES46&17400/400,MES46&377
	.BYTE MES50&17400/400,MES50&377
	.BYTE MES52&17400/400,MES52&377
	.BYTE MES54&17400/400,MES54&377
	.BYTE MES56&17400/400,MES56&377
	.BYTE MES50&17400/400,MES50&377
	.BYTE MES52&17400/400,MES52&377
	.BYTE MES54&17400/400,MES54&377
	.BYTE MES56&17400/400,MES56&377
	.BYTE MES70&17400/400,MES70&377
	.BYTE MES72&17400/400,MES72&377
	.BYTE MES74&17400/400,MES74&377
	.BYTE MES76&17400/400,MES76&377
	.BYTE MES100&17400/400,MES100&377
	.BYTE MES34&17400/400,MES34&377
	.BYTE MES0&17400/400,MES0&377
	.BYTE MES106&17400/400,MES106&377
	.BYTE MES110&17400/400,MES110&377
MOUTBL:	.BYTE DP+4&17400/400,DP+4&377
	.BYTE DP+115&17400/400,DP+115&377
	.BYTE DP+115&17400/400,DP+115&377
	.BYTE DP+407&17400/400,DP+407&377
	.BYTE DP+115&17400/400,DP+115&377
	.BYTE DP+407&17400/400,DP+407&377
	.BYTE DP+506&17400/400,DP+506&377
	.BYTE DP+505&17400/400,DP+505&377
	.BYTE DP+115&17400/400,DP+115&377
	.BYTE DP+521&17400/400,DP+521&377
	.BYTE DP+104&17400/400,DP+104&377
	.BYTE DP+146&17400/400,DP+146&377
	.BYTE DP+201&17400/400,DP+201&377
	.BYTE DP+301&17400/400,DP+301&377
	.BYTE DP+26&17400/400,DP+26&377
	.BYTE DP+241&17400/400,DP+241&377
	.BYTE DP+424&17400/400,DP+424&377
	.BYTE DP+500&17400/400,DP+500&377
	.BYTE DP+521&17400/400,DP+521&377
	.BYTE DP+152&17400/400,DP+152&377
	.BYTE DP+104&17400/400,DP+104&377
	.BYTE DP+104&17400/400,DP+104&377
	.BYTE DP+104&17400/400,DP+104&377
	.BYTE DP+103&17400/400,DP+103&377
	.BYTE DP+126&17400/400,DP+126&377
	.BYTE DP+126&17400/400,DP+126&377
	.BYTE DP+126&17400/400,DP+126&377
	.BYTE DP+125&17400/400,DP+125&377
	.BYTE DP+14&17400/400,DP+14&377
	.BYTE DP+506&17400/400,DP+506&377
	.BYTE DP+610&17400/400,DP+610&377
	.BYTE DP+601&17400/400,DP+601&377
	.BYTE DP+700&17400/400,DP+700&377
	.BYTE DP+4&17400/400,DP+4&377
	.BYTE DP+26&17400/400,DP+26&377
	.BYTE DP+410&17400/400,DP+410&377
	.BYTE DP+52&17400/400,DP+52&377

	;WRONG
MES0:	.BYTE 240,227,222,217,216,207,240,EOT
	;SPORTS
MES2:	.BYTE 40,23,20,17,22,24,23,40,40,EOT
	;MOVIES
MES4:	.BYTE 40,15,17,26,11,5,23,40,40,EOT
	;25 CENTS PER GAME
MES6:	.BYTE 62,65,40,3,5,16,24,23,40,20,5,22,40,7,1,15,5,EOT
	;PEOPLE
MES10:	.BYTE 40,20,5,17,20,14,5,40,40,EOT
	;CHOOSE A CATEGORY
MES12:	.BYTE 3,10,17,17,23,5,40,1,40,3,1,24,5,7,17,22,31,EOT
	;PUSH 1 PLAYER START
MES14:	.BYTE 20,25,23,10,40,61,40,20,14,1,31,5,22,40,23,24,1,22,24,EOT
	;PUSH 1 OR 2 PLAYER START
MES16:	.BYTE 20,25,23,10,40,61,40,17,22,40,62,40,20,14,1,31
	.BYTE 5,22,40,23,24,1,22,24,EOT
	;POTPOURRI
MES20:	.BYTE 40,20,17,24,20,17,25,22,11,EOT
	;FOR 2000 SCORE
MES22:	.BYTE 40,6,17,22,40,63,60,60,60,40,23,3,17,22,5,EOT
	;CATEGORY:
MES24:	.BYTE 3,1,24,5,7,17,22,31,72,40,EOT
	;QUESTION TIMER: 500
MES26:	.BYTE 21,25,5,23,24,11,17,16,40,24,11,15,5,22
	.BYTE 72,40,65,60,60,40,EOT
	;ASTERISKS
MES30:	.BYTE 152,40,152,40,152,40,152,40,152,40,152,40,152,40
	.BYTE 152,40,152,40,152,40,152,40,152,40,152,40,152,40,152,40,EOT
	;RIGHT
MES34:	.BYTE 240,322,311,307,310,324,240,EOT
	;THE KEE GAMES QUIZ SHOW
MES36:	.BYTE 152,40,240,224,210,205,240,213,205,205,240,207,201,215,205,223,240
	.BYTE 221,225,211,232,240,223,210,217,227,240,40,152,EOT
	;PLAYER
MES40:	.BYTE 20,14,1,31,5,22,EOT
	;BONUS QUESTIONS
MES42:	.BYTE 40,40,2,17,16,25,23,40,21,25,5,23,24,11,17,16,23,EOT
	;FOR 4000 SCORE
MES44:	.BYTE 40,6,17,22,40,64,60,60,60,40,23,3,17,22,5,EOT
	;GAME OVER
MES46:	.BYTE 240,307,301,315,305,240,317,326,305,322,240,EOT
	;DUNCE
MES50:	.BYTE 4,25,16,3,5,EOT
	;LUCKY
MES52:	.BYTE 14,25,3,13,31,EOT
	;SMART
MES54:	.BYTE 23,15,1,22,24,EOT
	;GENIUS
MES56:	.BYTE 240,207,205,216,211,225,223,240,EOT
	;BONUS
MES70:	.BYTE 240,202,217,216,225,223,240,EOT
	;TEST YOUR KNOWLEDGE!
MES72:	.BYTE 24,5,23,24,40,31,17,25,22,40,13,16,17,27,14,5,4,7,5,41,EOT
	;TEST A FRIEND'S!
MES74:	.BYTE 24,5,23,24,40,1,40,6,22,11,5,16,4,47,23,41,EOT
	;RIGHT ANSWER GETS TIMER POINTS
MES76:	.BYTE 22,11,7,10,24,40,1,16,23,27,5,22,40
	.BYTE 7,5,24,23,40,24,11,15,5,22,40,20,17,11,16,24,23,EOT
	;2 SEC. PENALTY FOR WRONG ANSWER
MES100:	.BYTE 62,40,23,5,3,56,40,20,5,16,1,14,24,31,40
	.BYTE 6,17,22,40,27,22,17,16,7,40,1,16,23,27,5,22,EOT
	;BONUS QUESTIONS
MES106:	.BYTE 102,117,116,125,123,40,121,125,105,123,124,111,117,116,123,EOT
	;KEE GAMES
MES110:	.BYTE 40,40,113,105,105,40,107,101,115,105,123,EOT
	.=25000

QUEST:	LODI R0,012		;PRESET PROG. STATUS
	LPSL
	LODI R0,1
	STRA R0,VIDIS		;DISABLE VIDEO
	EORZ R0			;CLEAR R0
	STRZ INDEX		;CLEAR INDEX
	LODI R0,40
Q1:	STRA INDEX,X,DISP+200	;CLEAR DISPLAY
	STRA INDEX,X,DISP+340
	BIRR INDEX,Q1		;LOOP CONDITION
	LODI R2,036
	EORZ R0
	STRA R2,DPTR
	STRA R0,DPTR+1
TSYNC:	LODA R3,TYM1
	LODA R2,TYM2
	EORZ R0
	STRA R0,VIDIS		;ENABLE VIDEO
	LODZ R3
T1:	TPSU 200 		;FIND SENSE LOW
	BCTR HI,T1
T2:	TPSU 200		;FIND SENSE HIGH
	BCTR LO,T2
T3:	BDRR R0,T4		;DECR. TIMER
	BCTR UNC,TSYNC		;RESTART IF TOO LONG
T4:	TPSU 200		;FIND SENSE LOW
	BCTR HI,T3
	COMZ R2
	BCTA GRTR,TSYNC		;RESTART IF TOO SHORT
TSTART:	BSTA UNC,CHAR		;FETCH CHAR.
	COMI DATA,START		;CHECK FOR START CHAR.
	BCFR EQU,TSTART
	BCTA UNC,TANSW		;(PATCH)
QWRIT:	LODI INDEX,200		;PRESET INDEX
QW1:	LODI R3,0
	STRA R3,VIDIS		;ENABLE VIDEO
	BSTA UNC,CHAR
	LODI R3,1
	STRA R3,VIDIS		;DISABLE VIDEO
	COMI DATA,CRLF		;CHECK FOR CRLF
	BCTR EQU,QW3
	STRA INDEX,NX,DPTR	;WRITE CHAR.
	TMI INDEX,37
	BCTR ZERO,QW1
	BIRR INDEX,QW4		;INCR. INDEX
	LODA R2,DPTR		;INCR. DISP. POINTER
	BIRR R2,QW2
QW2:	STRA R2,DPTR
	BCTR UNC,QW4
QW3:	LODA R2,DPTR		;FETCH DISP.POINTER
	ADDI INDEX,40		;ADD 40 TO INDEX
	ADDI R2,0		;ADD CARRY TO DPTR
	STRA R2,DPTR		;STORE DPTR
	ANDI INDEX,340		;TRUNCATE INDEX
QW4:	COMI INDEX,340		;CHECK FOR INDEX > OR = TO 340
	BCTR LESS,QW1
	LODA R2,DPTR		;FETCH DISP.POINTER
	COMI R2,037		;CHECK FOR DPTR > OR = TO 37
	BCTR LESS,QW1
	EORZ R0			;ENABLE
	STRA R0,VIDIS
	RETC UNC
TANSW:	BSTR UNC,CHAR		;GET ANSWER CHAR.
	TMI R0,60		; TEST FOR "11" IN BITS 4 & 5
	BCFA MATCH,TSYNC
	STRZ R2			;STORE BITS 0 & 1 IN R2
	ANDI R2,003
	RRR  DATA
	RRR  DATA		;STORE BITS 2 & 3 IN R3
	STRZ R3
	ANDI R3,003
	LODZ R3			;CHECK FOR R2=R3
	EORZ R2
	BRNA R0,TSYNC
	BIRR R3,S,+2
	LODI R0,1
	STRA R0,VIDIS		;DISABLE VIDEO
	STRA R3,ANSW		;STORE ANSW.
	EORZ R0
	STRA R0,VIDIS		;ENABLE VIDEO
	BCTA UNC,QWRIT		;(RETURN FROM PATCH)
CHAR:	EORZ R0
	PPSL 32			;REG. BANK 1, W/C, LOG. COM.
	LODI R1,6		;SET LOOP CTR.
C1:	BSTR UNC,BIT		;FETCH BIT
	BRNR R3,C2
	COMI R1,6
	BCTR EQU,C1
	BCTR UNC,CEXIT
C2:	RRR R3			;SHIFT BIT "1" INTO CARRY
	RRR R3
	RRL  DATA		;SHIFT CARRY INTO R0
	BDRR R1,C1		;LOOP BACK
C3:	BSTR UNC,BIT		;FETCH BIT
	BIRR R1,S,+2
	BRNR R3,C2
CEXIT:	CPSL 20			;REG. BANK 0
	EORI R0,77
	RETC UNC
BIT:	LODA R2,BTYM
	LODI R3,0
	LODI R3,377
LEDG:	TPSU 200
	BCTR HI,LEDG
EDG:	TPSU 200
	BCFR HI,EDG
HIGH:	BDRR R2,S,+3
	RETC UNC
	TPSU 200
	BCTR HI,HIGH
	BDRR R2,S,+3
	RETC UNC
	TPSU 200
	BCTR HI,HIGH
	BIRR R3,S,+2
LOW:	BDRR R2,S,+3
	RETC UNC
	TPSU 200
	BCTR LO,LOW
	BDRR R2,S,+3
	RETC UNC
	TPSU 200
	BCTR LO,LOW
	BIRR R3,HIGH
TYM1:	.BYTE 22
TYM2:	.BYTE 6
BTYM:	.BYTE 23
	.END  MAINLP
                                                                                                                                                                                                 